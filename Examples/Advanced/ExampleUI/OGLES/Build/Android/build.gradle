import org.apache.tools.ant.taskdefs.condition.Os
	
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

apply plugin: 'com.android.application'

File pvrSdkDir = file("${project.buildDir}/../../../../../../../")

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.powervr.OGLESExampleUI"
        minSdkVersion 16
        targetSdkVersion 21
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ext{
                ndkArgs=['-j8']
            }

        }
        debug {
            jniDebuggable true
            ext {
                ndkArgs = ['-j8', 'DEBUG=1', 'NDK_DEBUG=1']
            }
        }
    }
    if (System.env.APP_ABI) {
        buildTypes.release.ndkArgs << ' APP_ABI='+System.env.APP_ABI + ''
        buildTypes.debug.ndkArgs << ' APP_ABI='+System.env.APP_ABI + ''
    }
	
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jni.srcDirs= [] //Disable built-in ndk building.
            jniLibs.srcDir 'libs'

        }
    }

//Copy all our asset files into the android assets folder.
//Add to this list files that will need to be read at runtime here: Textures, shaders, text files etc.
FileCollection assetFiles = files(
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/container-corner.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/container-filler.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/container-vertical.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/container-horizontal.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/topbar.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/background.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/clock-face.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/clock-face-small.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/hand.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/hand-small.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/battery.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/internet-web-browser.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/mail-message-new.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/network-wireless.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/office-calendar.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/weather-sun-cloud.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/weather-storm.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/weather-rain.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/text1.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/text2.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/text-weather.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/text-fri.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/text-sat.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/text-sun.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/text-mon.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/weather-sun-cloud-big.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/window-bottom.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/window-bottomcorner.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/window-side.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/window-top.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/window-topleft.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/window-topright.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/loremipsum.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/ProcessedAssets/vertical-bar.pvr",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/OGLES/TexColShaderF_ES2.fsh",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/OGLES/TexColShaderF_ES3.fsh",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/OGLES/TexColShaderV_ES2.vsh",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/OGLES/TexColShaderV_ES3.vsh",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/OGLES/ColShaderF_ES2.fsh",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/OGLES/ColShaderF_ES3.fsh",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/OGLES/ColShaderV_ES2.vsh",
"${pvrSdkDir}/Examples/Advanced/ExampleUI/OGLES/ColShaderV_ES3.vsh",

)

    String ndkBuildCommand = 'ndk-build';

    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        ndkBuildCommand = 'ndk-build.cmd'
    }

	//Runs before ndk building tasks
    task copyAssets(type: Copy){
        from assetFiles
        into 'assets'
    }

	//Main tasks for ndk Building of the application. Dependencies will be added to framework projects later
    task ndkBuildDebug(type: Exec, dependsOn:[copyAssets]) {
        executable ndkBuildCommand
		args buildTypes.debug.ndkArgs
    }

    task ndkBuildRelease(type: Exec, dependsOn:[copyAssets]) {
        executable ndkBuildCommand
		args buildTypes.release.ndkArgs
    }

    task ndkBuildClean(type: Exec) {
        executable ndkBuildCommand
		args buildTypes.release.ndkArgs + 'clean'
    }

    //In order to shorten this file a bit, create the NDK Build tasks dynamically for each Framework project.
    // We will be creating a debug and a release task for each PVR Framework project dependency.
	// Otherwise we would need to add 3 tasks per project... (build debug, build release, clean...)

    def ndkDependencies=[
    ["PVRCore", "${pvrSdkDir}/Framework/PVRCore/Build/Android"],
    ["PVRAssets", "${pvrSdkDir}/Framework/PVRAssets/Build/Android"],
    ["PVRPlatformGlue", "${pvrSdkDir}/Framework/PVRPlatformGlue/EGL/Build/Android"],
    ["PVRGles", "${pvrSdkDir}/Framework/PVRApi/OGLES/Build/Android"],
    ["PVRShell", "${pvrSdkDir}/Framework/PVRShell/Build/Android"],
    ["PVRUIRenderer", "${pvrSdkDir}/Framework/PVRUIRenderer/Build/Android"]
    ]

	//Create and configure a task for each combination of build type (debug/release or clean) and dependency
	//For example, there will be task for ndkBuildPVRAssetsDebug, ndkBuildPVRAssetsRelease, ndkBuildPVRAssetsClean
    android.buildTypes.all{ theBuildType -> //Debug/Release
        ndkDependencies.each() { 
            project -> //The list above ProjectName, ProjectPath
            String projectName = "ndkBuild${project[0]}${theBuildType.name.capitalize()}" //e.g. ndkBuildPVRCoreDebug
            task(projectName, type: Exec) {
                workingDir "${project[1]}"
                executable ndkBuildCommand
                args theBuildType.ext.ndkArgs
            }
            task(projectName + "Clean", type: Exec) { //e.g. ndkBuildPVRCoreClean
                workingDir "${project[1]}"
                executable ndkBuildCommand
                args theBuildType.ext.ndkArgs + 'clean'
            }
            tasks.findByName("ndkBuild${theBuildType.name.capitalize()}").dependsOn projectName
			clean.dependsOn projectName + "Clean"
        }
    }

	    tasks.withType(JavaCompile) {
        theTask ->
            buildTypes.each() { theBuildType ->
                if(theTask.name.contains("compile${theBuildType.name.capitalize()}Java")){
                    theTask.dependsOn "ndkBuild${theBuildType.name.capitalize()}"
                }
            }
    }

	//Copy outputs to the bin/ folder. Less idiomatic, more understandable...
	task copyToBin(type: Copy) {
        from 'build/outputs/apk/'
        into 'bin/'
        include('*.apk')
   }

    assembleRelease {
        doLast() {
            copyToBin.execute()
        }
    }

    clean.dependsOn ndkBuildClean
    clean {
        doLast() {
            delete "assets"
            delete "obj"
            delete "bin"
        }
    }

    tasks.withType(Exec) {
        doFirst {
            println commandLine
        }
    }
    preBuild.dependsOn copyAssets
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
