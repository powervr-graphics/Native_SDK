import org.apache.tools.ant.taskdefs.condition.Os
	
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

apply plugin: 'com.android.application'

File pvrSdkDir = file("${project.buildDir}/../../../../../../../")

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "com.powervr.OGLESPVRScopeRemote"
        minSdkVersion 16
        targetSdkVersion 24
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ext{
                ndkArgs=['-j8']
            }

        }
        debug {
            jniDebuggable true
            ext {
                ndkArgs = ['-j8', 'DEBUG=1', 'NDK_DEBUG=1']
            }
        }
    }
    if (System.env.APP_ABI) {
        buildTypes.release.ndkArgs << ' APP_ABI='+System.env.APP_ABI + ''
        buildTypes.debug.ndkArgs << ' APP_ABI='+System.env.APP_ABI + ''
    }
	
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jni.srcDirs= [] //Disable built-in ndk building.
            jniLibs.srcDir 'libs'

        }
    }

//Copy all our asset files into the android assets folder.
//Add to this list files that will need to be read at runtime here: Textures, shaders, text files etc.
FileCollection assetFiles = files(
"${pvrSdkDir}/Examples/Advanced/PVRScopeRemote/ProcessedAssets/Marble.pvr",
"${pvrSdkDir}/Examples/Advanced/PVRScopeRemote/OGLES/FragShader_ES2.fsh",
"${pvrSdkDir}/Examples/Advanced/PVRScopeRemote/OGLES/FragShader_ES3.fsh",
"${pvrSdkDir}/Examples/Advanced/PVRScopeRemote/OGLES/VertShader_ES2.vsh",
"${pvrSdkDir}/Examples/Advanced/PVRScopeRemote/OGLES/VertShader_ES3.vsh",
"${pvrSdkDir}/Examples/Advanced/PVRScopeRemote/ProcessedAssets/scene.pod",

)

    String ndkBuildCommand = 'ndk-build';

    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        ndkBuildCommand = 'ndk-build.cmd'
    }

	//Runs before ndk building tasks
    task copyAssets(type: Copy){
        from assetFiles
        into 'assets'
    }

	//Main tasks for ndk Building of the application. Dependencies will be added to framework projects later
    task ndkBuildDebug(type: Exec, dependsOn:[copyAssets]) {
        executable ndkBuildCommand
		args buildTypes.debug.ndkArgs
    }

    task ndkBuildRelease(type: Exec, dependsOn:[copyAssets]) {
        executable ndkBuildCommand
		args buildTypes.release.ndkArgs
    }

    task ndkBuildClean(type: Exec) {
        executable ndkBuildCommand
		args buildTypes.release.ndkArgs + 'clean'
    }

    //In order to shorten this file a bit, create the NDK Build tasks dynamically for each Framework project.
    // We will be creating a debug and a release task for each PVR Framework project dependency.
	// Otherwise we would need to add 3 tasks per project... (build debug, build release, clean...)

    def ndkDependencies=[
    ["PVRCore", "${pvrSdkDir}/Framework/PVRCore/Build/Android"],
    ["PVRAssets", "${pvrSdkDir}/Framework/PVRAssets/Build/Android"],
    ["PVRNativeGles", "${pvrSdkDir}/Framework/PVRNativeApi/OGLES/Build/Android"],
    ["PVRGles", "${pvrSdkDir}/Framework/PVRApi/OGLES/Build/Android"],
    ["PVRShell", "${pvrSdkDir}/Framework/PVRShell/Build/Android"],
    ["PVREngineUtils", "${pvrSdkDir}/Framework/PVREngineUtils/Build/Android"]
    ]

	//Create and configure a task for each combination of build type (debug/release or clean) and dependency
	//For example, there will be task for ndkBuildPVRAssetsDebug, ndkBuildPVRAssetsRelease, ndkBuildPVRAssetsClean
    android.buildTypes.all{ theBuildType -> //Debug/Release
        ndkDependencies.each() { 
            project -> //The list above ProjectName, ProjectPath
            String projectName = "ndkBuild${project[0]}${theBuildType.name.capitalize()}" //e.g. ndkBuildPVRCoreDebug
            task(projectName, type: Exec) {
                workingDir "${project[1]}"
                executable ndkBuildCommand
                args theBuildType.ext.ndkArgs
            }
            task(projectName + "Clean", type: Exec) { //e.g. ndkBuildPVRCoreClean
                workingDir "${project[1]}"
                executable ndkBuildCommand
                args theBuildType.ext.ndkArgs + 'clean'
            }
            tasks.findByName("ndkBuild${theBuildType.name.capitalize()}").dependsOn projectName
			clean.dependsOn projectName + "Clean"
        }
    }

	    tasks.withType(JavaCompile) {
        theTask ->
            buildTypes.each() { theBuildType ->
                if(theTask.name.contains("compile${theBuildType.name.capitalize()}Java")){
                    theTask.dependsOn "ndkBuild${theBuildType.name.capitalize()}"
                }
            }
    }

	//Copy outputs to the bin/ folder. Less idiomatic, more understandable...
	task copyToBin(type: Copy) {
        from 'build/outputs/apk/'
        into 'bin/'
        include('*.apk')
   }

    assembleRelease {
        doLast() {
            copyToBin.execute()
        }
    }

    clean.dependsOn ndkBuildClean
    clean {
        doLast() {
            delete "assets"
            delete "obj"
            delete "bin"
        }
    }

    tasks.withType(Exec) {
        doFirst {
            println commandLine
        }
    }
    preBuild.dependsOn copyAssets
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
